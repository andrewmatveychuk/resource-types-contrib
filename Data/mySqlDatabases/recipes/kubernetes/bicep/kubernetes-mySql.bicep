@description('Information about what resource is calling this Recipe. Generated by Radius. For more information visit https://docs.radapp.io/reference/context-schema/ ')
param context object

extension kubernetes with {
  kubeConfig: ''
  namespace: context.runtime.kubernetes.namespace
} as kubernetes

@description('Name of the MySQL database. Defaults to the application name.')
param database string = context.application.name

@description('MySQL username. Defaults to <application-name>-user')
param user string = '${context.application.name}-user'

@description('MySQL user password. Defaults to a unique generated value.')
@secure()
param password string = uniqueString(context.resource.id, newGuid())

@description('MySQL root user password. Defaults to a unique generated value.')
@secure()
param root_password string = uniqueString(context.resource.id, newGuid())

@description('Tag to pull for the mysql container image. Defaults to latest.')
param tag string = 'latest'


var uniqueName = 'mysql-${uniqueString(context.resource.id)}'
var mySqlImage = 'mysql:${tag}'
var port = 3306

resource mySql 'apps/Deployment@v1' = {
  metadata: {
    name: uniqueName
  }
  spec: {
    selector: {
      matchLabels: {
        app: context.application.name
        resource: context.resource.name
      }
    }
    template: {
      metadata: {
        labels: {
          app: context.application.name
          resource: context.resource.name
          // 'radapp.io/application': context.application == null ? '' : context.application.name
        }
      }
      spec: {
        containers: [
          {
            name: 'mysql'
            image: mySqlImage
            ports: [
              {
                containerPort: port
              }
            ]
            env: [
              {
                name: 'MYSQL_ROOT_PASSWORD'
                value: root_password
              }
              {
                name: 'MYSQL_USER'
                value: user
              }
              {
                name: 'MYSQL_PASSWORD'
                value: password
              }
              {
                name: 'MYSQL_DATABASE'
                value: database
              }
            ]
          }
        ]
      }
    }
  }
}

resource svc 'core/Service@v1' = {
  metadata: {
    name: uniqueName
    labels: {
      app: context.application.name
    }
  }
  spec: {
    type: 'ClusterIP'
    selector: {
      app: context.application.name
      resource: context.resource.name
    }
    ports: [
      {
        port: port
      }
    ]
  }
}

output result object = {
  resources: [
    '/planes/kubernetes/local/namespaces/${svc.metadata.namespace}/providers/core/Service/${svc.metadata.name}'
    '/planes/kubernetes/local/namespaces/${mySql.metadata.namespace}/providers/apps/Deployment/${mySql.metadata.name}'
  ]
  values: {
    host: '${svc.metadata.name}.${svc.metadata.namespace}.svc.cluster.local'
    port: port
    database: database
    user: user
  }
  secrets: {
    #disable-next-line outputs-should-not-contain-secrets
    password: password
    #disable-next-line outputs-should-not-contain-secrets
    root_password: root_password
  }
}
